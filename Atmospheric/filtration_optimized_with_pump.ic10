##########################################################################################
# This Script is used to control a filtration unit. The following optimizations are made:
# 1. The filtration only runs if the pressure is above a certain pressure threshold. To
#    increase the pressure in front of the filtration unit a pump is used.
# 2. Cut of filtering if the filters are nearly empty.
##########################################################################################
# Names for devices (name them with labeler)
# FILTRATION_EXHAUST
# PUMP_EXHAUST
# TANK_AIR_FILTERED
# ANALYSER_EXHAUST
##########################################################################################
alias filterUnit db # db refers to the filtration unit
alias inputPressure r0 # pressure before filter
alias inputPressurePump r1 # pressure before pump
alias outputPressure1 r2 # filtered air
alias outputPressure2 r3 # waste
alias filter0Remains r4
alias filter1Remains r5
#
define MINPRESSURE 1000 # 1MPa, minimum pressure to start filtering
define MINPRESSUREPUMP 10000 # 2MPa, minimum pressure to run the pump
define MAXPRESSURE 40000 # 40MPa, maximum pressure before stopping the pump
##########################################################################################


main:
yield
l inputPressure filterUnit PressureInput
l outputPressure1 filterUnit PressureOutput # filtered air
l outputPressure2 filterUnit PressureOutput2 # waste (not needed here)
ls filter0Remains filterUnit 0 Quantity # 0 = Gas Filter A
ls filter1Remains filterUnit 1 Quantity # 1 = Gas Filter B
lbn inputPressurePump 435685051 HASH("ANALYSER_EXHAUST") Pressure Average
sbn 435685051 HASH("ANALYSER_EXHAUST") On 1 # ensure analyser is on

### cut of filtering if the filters are nearly empty
ble filter0Remains 2 stopFilteringCutOff # a <= b
ble filter1Remains 2 stopFilteringCutOff # a <= b
### cut of filtering if output pressure is too high
bgt outputPressure1 MAXPRESSURE stopFilteringCutOff # a > b
bgt outputPressure2 MAXPRESSURE stopFilteringCutOff # (not needed here)
### cut of all if input pressure to low for pump
bleal inputPressurePump 100 stopFilteringCutOff # a <= b

### increase pressure with pump
s db Setting inputPressure
bltal inputPressure MINPRESSUREPUMP startPump # a <= b
bgtal inputPressure MAXPRESSURE stopPump # a > b

### check if input pressure is sufficient
bleal inputPressure MINPRESSURE stopFiltering # a <= b
bgtal inputPressure MINPRESSURE startFiltering # a > b
j main

stopFilteringCutOff:
sbn 1310794736 HASH("PUMP_EXHAUST") On 0 # turn off pump
s filterUnit Mode 0
j main

startPump:
sbn 1310794736 HASH("PUMP_EXHAUST") Mode 0 # DIRECTION?
sbn 1310794736 HASH("PUMP_EXHAUST") Setting 10 # SPEED?
sbn 1310794736 HASH("PUMP_EXHAUST") On 1 # turn on pump
j ra


stopPump:
sbn 1310794736 HASH("PUMP_EXHAUST") On 0 # turn off pump
j ra

stopFiltering:
s filterUnit Mode 0
j ra

startFiltering:
s filterUnit Mode 1
j ra